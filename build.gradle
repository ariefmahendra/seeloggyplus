plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.runtime' version '1.13.1'
}

group = 'com.seeloggyplus'
// The project version is now defined in the gradle.properties file.

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

javafx {
    version = '21'
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

dependencies {
    implementation 'com.jcraft:jsch:0.1.55'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation("com.github.ben-manes.caffeine:caffeine:3.2.2")
    implementation 'org.xerial:sqlite-jdbc:3.44.1.0'
    implementation 'org.dom4j:dom4j:2.2.0'
    implementation 'org.slf4j:slf4j-api:2.0.9'
    implementation 'ch.qos.logback:logback-classic:1.5.19'
    implementation 'commons-io:commons-io:2.14.0'
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'org.controlsfx:controlsfx:11.1.2'
    implementation 'org.fxmisc.richtext:richtextfx:0.11.2'
    implementation 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2'
    implementation 'com.unboundid:unboundid-ldapsdk:7.0.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

// Generates a version.properties file from the project version
task generateVersionProperties {
    doLast {
        def propsFile = file("$buildDir/resources/main/version.properties")
        propsFile.parentFile.mkdirs()
        propsFile.text = "version=${project.version}"
    }
}
// Ensure the properties file is generated before resources are processed
processResources.dependsOn generateVersionProperties

application {
    mainClass = 'com.seeloggyplus.Main'
    applicationName = 'SeeloggyPlus'
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = [
            'java.desktop', 'java.logging', 'java.naming', 'java.sql', 'java.xml',
            'jdk.unsupported', 'java.scripting', 'java.management',
            'jdk.crypto.ec', 'jdk.crypto.cryptoki',
            'javafx.controls', 'javafx.fxml', 'javafx.web', 'javafx.graphics'
    ]

    targetPlatform('win') {
        jdkHome = jdkDownload('https://cdn.azul.com/zulu/bin/zulu17.46.19-ca-fx-jdk17.0.9-win_x64.zip')
    }
}
tasks.register('fatJar', Jar) {
    archiveClassifier = 'all'
    archiveFileName = "seeloggyplus-all.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest { attributes 'Main-Class': 'com.seeloggyplus.Launcher' }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.register('createPortableDist', Zip) {
    group = 'distribution'
    description = 'create zip portable distribution'

    dependsOn 'fatJar', 'jpackageImage'

    archiveFileName = "${project.name}-Portable.zip"
    destinationDirectory = file("${buildDir}/distributions")

    into("${project.name}") {
        from("${buildDir}/jpackage/${project.name}/runtime") {
            into 'jre'
        }

        from(tasks.fatJar.archiveFile) {
            rename { "${project.name}.jar" }
        }

        def jvmArgs = '--add-opens javafx.graphics/javafx.scene=ALL-UNNAMED ' +
                '--add-opens javafx.controls/javafx.scene.control=ALL-UNNAMED ' +
                '--add-opens javafx.controls/javafx.scene.control.skin=ALL-UNNAMED ' +
                '--add-opens javafx.base/com.sun.javafx.runtime=ALL-UNNAMED ' +
                '--add-opens javafx.graphics/com.sun.javafx.scene=ALL-UNNAMED ' +
                '--add-opens javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED'

        def scriptNoConsole = """
@echo off
set DIR=%~dp0
start "" "%DIR%jre\\bin\\javaw.exe" ${jvmArgs} -jar "%DIR%${project.name}.jar"
exit
        """.trim()

        from(resources.text.fromString(scriptNoConsole)) {
            rename { "seeloggyplus-start.bat" }
        }

        def scriptWithConsole = """
@echo off
title Debug Mode - ${project.name}
echo Starting application...
echo ---------------------------------------------------
set DIR=%~dp0
"%DIR%jre\\bin\\java.exe" ${jvmArgs} -jar "%DIR%${project.name}.jar"
echo ---------------------------------------------------
echo Application is closed.
pause
        """.trim()

        from(resources.text.fromString(scriptWithConsole)) {
            rename { "seeloggyplus-debug.bat" }
        }
    }
}